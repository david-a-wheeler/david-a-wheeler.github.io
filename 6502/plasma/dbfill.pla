CONST SHOWGR    = $C050
CONST SHOWFULL  = $C052
CONST SHOWPG1   = $C054
CONST SHOWPG2   = $C055
CONST SHOWLORES = $C056
BYTE EDGETP     = 0
BYTE EDGEBT     = 47
BYTE EDGELT[48]
BYTE EDGERT[48]

WORD VERTX[3] = [400, 170, 320]
WORD VERTY[3] = [700, 230,  80]
WORD INCX[3]  = [ 20,   8,  -7]
WORD INCY[3]  = [ -7,  20,  -4]

BYTE C

DEF TEXTMODE
  ^SHOWPG1
  RETURN ROMCALL(0, 0, 0, 0, $FB39)
END

DEF CPYBUF
  RETURN MEMCPY($0400, $0800, 1016)
END

DEF GRMODE
  ROMCALL(0, 0, 0, 0, $F832)
  ^SHOWGR
  ^SHOWFULL
  CPYBUF
  ^SHOWPG2
  RETURN ^SHOWLORES
END

DEF COLOR(CLR)
  RETURN ROMCALL(CLR, 0, 0, 0, $F864)
END

DEFN FILLSTART
  BYTE Y

  FOR Y = EDGETP TO EDGEBT
    EDGELT[Y] = 40
    EDGERT[Y] = 0
  NEXT
  EDGETP = 47
  EDGEBT = 0
END

DEFT FILL
  BYTE Y

  FOR Y = EDGETP TO EDGEBT
    IF EDGERT[Y] >= EDGELT[Y]
      ^($2C) = EDGERT[Y]
      ROMCALL(Y, 0, EDGELT[Y], 0, $F819)
    FIN
  NEXT
END

DEFN ADDPT(X, Y)
  IF Y >= 0 AND Y <= 47
    IF Y < EDGETP
      EDGETP = Y
    FIN
    IF Y > EDGEBT
      EDGEBT = Y
    FIN
    IF X < 0
      X = 0
    ELSIF X > 39
      X = 39
    FIN
    IF X < EDGELT[Y]
      EDGELT[Y] = X
    FIN
    IF X > EDGERT[Y]
      EDGERT[Y] = X
    FIN
  FIN
END

DEFN EDGE(X1, Y1, X2, Y2)
  BYTE DX, DY, DX2, DY2
  WORD SX, SY, ERR, ERR2

  IF X1 < X2
    SX = 1
    DX = X2 - X1
  ELSE
    SX = -1
    DX = X1 - X2
  FIN
  IF Y1 < Y2
    SY = 1
    DY = Y2 - Y1
  ELSE
    SY = -1
    DY = Y1 - Y2
  FIN
  ADDPT(X1, Y1)
  IF X1 <> X2 OR Y1 <> Y2
    DX2 = DX << 1
    DY2 = DY << 1
    ERR = DX2 - DY2
    REPEAT
      ERR2 = ERR
      IF ERR2 < DX
        ADDPT(X1, Y1)
        ERR = ERR + DX2
        Y1 = Y1 + SY
      FIN
      IF ERR2 > -DY
        ERR = ERR - DY2
        X1 = X1 + SX
      FIN
    UNTIL X1 == X2 AND Y1 == Y2
    ADDPT(X2, Y2)
  FIN
END

DEFT UPDTVERTS
  BYTE I

  FOR I = 0 TO 2
    VERTX[I] = VERTX[I] + INCX[I]
    IF VERTX[I] < 0
      VERTX[I] = 0
      INCX[I]  = -INCX[I]
    ELSIF VERTX[I] > 635
      VERTX[I] = 635
      INCX[I] = -INCX[I]
    FIN
    VERTY[I] = VERTY[I] + INCY[I]
    IF VERTY[I] < 0
      VERTY[I] = 0
      INCY[I]  = -INCY[I]
    ELSIF VERTY[I] > 751
      VERTY[I] = 751
      INCY[I]  = -INCY[I]
    FIN
  NEXT
END

DEFT FILLPOLY
  FILLSTART
  EDGE(VERTX[0] >> 4, VERTY[0] >> 4, VERTX[1] >> 4, VERTY[1] >> 4)
  EDGE(VERTX[1] >> 4, VERTY[1] >> 4, VERTX[2] >> 4, VERTY[2] >> 4)
  EDGE(VERTX[2] >> 4, VERTY[2] >> 4, VERTX[0] >> 4, VERTY[00] >> 4)
  FILL
END

GRMODE
REPEAT
  FOR C = 1 TO 15
    UPDTVERTS
    COLOR(C)
    FILLPOLY
    CPYBUF
    COLOR(0)
    FILL
  NEXT
UNTIL ^$C000 > 127
TEXTMODE
^$C010
DONE
